title: START
tags: 
colorID: 0
position: 656,29
---
have you ever wondered what a dream could mean?
-> Yeah, sometimes.
    <<jump WONDER>>
-> Not really.
    <<jump WONDER>>
===
title: WONDER
tags: 
colorID: 0
position: 862,-89
---
It can be fun to think about! Do you think you could remember a recent dream?
-> Yes, I have one in mind.
    <<jump DREAM>>
-> No, I can't think of any.
    <<jump NODREAM>>
===
title: DREAM
tags: 
colorID: 0
position: 982,-309
---
That's great, well I am your dream journal, so let's try writing about it.
<<jump JOURNAL>>
===
title: NODREAM
tags: 
colorID: 0
position: 1202,-309
---
That's too bad, well, journaling can sometimes help people remember their dreams.
We can still give it a try by answering questions about your dreams in general.
<<jump JOURNAL>>
===
title: JOURNAL
tags: 
colorID: 0
position: 1102,-529
---
How were you feeling during the dream?
-> I felt good.
    <<set $dream_mood to "positive">>
    <<jump SLEEPQUALITY>>
-> I felt fine.
    <<set $dream_mood to "neutral">>
    <<jump SLEEPQUALITY>>
-> I felt bad.
    <<set $dream_mood to "negative">>
    <<jump SLEEPQUALITY>>
===
title: SLEEPQUALITY
tags: 
colorID: 0
position: 1328,-678
---
<<if $dream_mood is "positive">>
    That's great!
<<elseif $dream_mood is "neutral">>
    Oh, interesting!
<<elseif $dream_mood is "negative" >>
    Oh no, I'm sorry!
<<endif>>
Did you sleep well that night?
-> Yes, I slept like a baby.
    <<set $dream_quality to "high">>
    <<jump PLACE>>
-> It was a normal night's sleep
    <<set $dream_quality to "average">>
    <<jump PLACE>>
-> No, I was tossing and turning all night.
    <<set $dream_quality to "low">>
    <<jump PLACE>>
===
title: PLACE
tags: 
colorID: 0
position: 1513,-845
---
<<if $dream_quality is "high">>
    You must feel well rested!
<<elseif $dream_quality is "average">>
    Glad, you got some rest!
<<elseif $dream_quality is "low" >>
    Hope you get some rest soon!
<<endif>>
Based on the events or things there, when did the dream seem to take place?
-> In the past.
    <<set $dream_time to "past">>
    <<jump PERSON>>
-> In the present.
    <<set $dream_time to "present">>
    <<jump PERSON>>
-> In the future.
    <<set $dream_time to "future">>
    <<jump PERSON>>
===
title: PERSON
tags: 
colorID: 0
position: 1679,-1010
---
Was anyone else in the dream?
-> There were current or former friends there.
    <<set $person to "friend">>
    <<jump PERSPECTIVE>>
-> My family was there.
    <<set $person to "family">>
    <<jump PERSPECTIVE>>
-> There were people I don't know very well or at all.
    <<set $person to "acquaintance">>
    <<jump PERSPECTIVE>>
===
title: PERSPECTIVE
tags: 
colorID: 0
position: 1896,-1170
---
Were you part of the dream or just observing?
-> I was part of the dream.
    <<set $perspective to "first">>
    <<jump INTROEND>>
-> I was just watching the dream, not partaking.
    <<set $perspective to "third">>
    <<jump INTROEND>>
-> I was in the dream, but watching myself as an outsider.
    <<set $perspective to "mixed">>
    <<jump INTROEND>>

===
title: INTROEND
tags: 
colorID: 0
position: 1896,-1170
---
Thanks for answering those questions!
Recalling dreams is a good start. Trying to understand them is another story.
<<load_game>>
===
title: JOURNAL1
tags: 
colorID: 0
position: 2246,174
---
How do you feel about the {$person} from your dream? Do you [select value={$dream_mood} positive="like them" neutral="not care for them" negative="dislike them" /]?
-> Yes.
    <<set $person_mood_connection to true>>
    <<jump PERSONPERSPECTIVE>>
-> No.
    <<set $person_mood_connection to false>>
    <<jump PERSONPERSPECTIVE>>
===
title: PERSONPERSPECTIVE
tags: 
colorID: 0
position: 2486,-267
---
Do you feel [select value={$perspective} first="close to" third="disconnected from" mixed="like your relationship is changing with" /] the {$person} in your dream?
-> Yes.
    <<set $person_pov_connection to true>>
    <<jump JOURNAL1END>>
-> No.
    <<set $person_pov_connection to false>>
    <<jump JOURNAL1END>>
===
title: JOURNAL1END
tags: 
colorID: 0
position: 2726,-709
---
<<if $person_mood_connection is true>>
    Since you felt {$dream_mood} towards the {$person} from your dream, that person may be related to the {$dream_mood} tone of your dream.
<<else>>
	Since you didn't feel {$dream_mood} towards the {$person} from your dream, that person doesn't seem to be related to the tone of your dream.
<<endif>>
The {$person} from your dream may represent a shared quality about yourself that you [select value={$dream_mood} positive="like" negative="dislike" neutral="have mixed feelings about" /].
<<if $person_pov_connection is true>>
	Since you felt [select value={$perspective} first="close to" third="disconnected from" mixed="like your relationship is changing with" /] the {$person} in your dream, they seem to be connected to the {$perspective}-person perspective of your dream.  
<<else>>
    You felt [select value={$perspective} first="close to" third="disconnected from" mixed="like your relationship is changing with" /] the {$person} in your dream, which suggests something different from the {$perspective}-person perspective of your dream.
<<endif>>
They could represent a part of your life or memory that you feel [select value={$perspective} first="is repeating itself or happening again" third="like you've moved on from" mixed="you miss" /].
<<close_journal JournalCheckpoint1>>
===
title: JOURNAL2
tags: 
colorID: 0
position: 3216,178
---
Do you feel [select value={$dream_mood} positive="optimistic about" neutral="confused about" negative="worried or unproud of" /] the {$dream_time}?
-> Yes.
    <<set $time_mood_connection to true>>
    <<jump TIMEPERSPECTIVE>>
-> No.
    <<set $time_mood_connection to false>>
    <<jump TIMEPERSPECTIVE>>
===

title: TIMEPERSPECTIVE
tags: 
colorID: 0
position: 3454,-261
---
Do you feel that the {$dream_time} event that took place in your dream [select value={$perspective} first="is vital" third="is unimportant" mixed="could be important" /] to who you are now?
-> Yes.
    <<set $time_pov_connection to true>>
    <<jump JOURNAL2END>>
-> No.
    <<set $time_pov_connection to false>>
    <<jump JOURNAL2END>>
===
title: JOURNAL2END
tags: 
colorID: 0
position: 3694,-701
---
<<if $time_mood_connection is true>>
	Since you felt {$dream_mood} towards the {$dream_time} setting of your dream, it seems to be related to the {$dream_mood} tone of your dream.
<<else>>
    Since you felt differently than {$dream_mood} towards the {$dream_time} setting of your dream, it doesn't seem to be related to the tone of your dream.
<<endif>>
The {$dream_time} may represent something that is making you feel [select value={$dream_mood} positive="happy" negative="sad" neutral="detached" /].
<<if $time_pov_connection is true>>
	Your perspective on the {$dream_time} setting of your dream seems to be connected to the {$perspective}-person perspective of your dream.
<<else>>
	Your perspective on the {$dream_time} setting of your dream doesn't seems to be connected to the {$perspective}-person perspective of your dream.
<<endif>>
 You may feel [select value={$perspective} first="fond of or excited for" third="like you've moved on from" mixed="unsure or confused about" /] the {$dream_time}.
<<close_journal JournalCheckpoint2>>
===
title: JOURNAL3
tags: 
colorID: 0
position: 4246,178
---
<<if $dream_quality is "high">>
    Given that you've been sleeping well, I would say that despite the things on your mind you are processing and dealing with them well. 
<<elseif $dream_quality is "low">>
    Given that you haven't been sleeping well, I would say that the things on your mind are bothering you and might feel unresolved.
<<else>>
	Since you've had average sleep, it's hard to say if these dreams represent things that are bothering you or not.
<<endif>>

<<if $person_mood_connection is true ||  $person_pov_connection is true ||  $time_mood_connection is true ||  $time_pov_connection is true>>
    It seems like there's some connections between the aspects you wrote about in your dreams. It can be helpful to try and think about what those things might mean to you.
<<endif>>

<<if $person_mood_connection is false && $person_pov_connection is false && $time_mood_connection is false && $time_pov_connection is false>>
    There didn't seem to be any connections between the aspects in your dream, and that's okay. There might be other parts of you dream that are worth thinking about.
<<endif>>
<<jump JOUNRAL3END>>
===
title: JOUNRAL3END
tags: 
colorID: 0
position: 4366,-42
---
We can't always control our dreams or our feelings.
But we can control how we respond to them and what they mean to us.
Writing in a journal can be a good way to do that!
<<close_journal JournalCheckpoint3>>
===
