title: START
tags: 
colorID: 0
position: 656,29
---
have you ever wondered what a dream could mean?
-> Yeah, sometimes.
    <<jump WONDER>>
-> Not really.
    <<jump WONDER>>
===
title: WONDER
tags: 
colorID: 0
position: 862,-89
---
It can be fun to think about! Do you think you could remember a recent dream?
-> Yes, I have one in mind.
    << jump DREAM>>
-> No, I can't think of any.
    << jump NODREAM>>
===
title: DREAM
tags: 
colorID: 0
position: 982,-309
---
That's great, well I am your dream journal, so let's try writing about it.
-> Okay, let's start.
    << jump JOURNAL>>
===
title: NODREAM
tags: 
colorID: 0
position: 1202,-309
---
That's too bad, well, journaling can sometimes help people remember their dreams. For now we can journal about general dreams or recurring thoughts.
-> okay, i'll try...
    << jump JOURNAL>>
===
title: JOURNAL
tags: 
colorID: 0
position: 1102,-529
---
How were you feeling during the dream?
-> I felt good.
    <<set $dream_mood to "positive">>
    << jump SLEEPQUALITY>>
-> I felt fine.
    <<set $dream_mood to "neutral">>
    << jump SLEEPQUALITY>>
-> I felt bad.
    <<set $dream_mood to "negative">>
    << jump SLEEPQUALITY>>
===
title: SLEEPQUALITY
tags: 
colorID: 0
position: 1328,-678
---
<<if $dream_mood is "positive">>
    That's great!
<<elseif $dream_mood is "neutral">>
    Oh, interesting!
<<elseif $dream_mood is "negative" >>
    Oh no, I'm sorry!
<<endif>>
Did you sleep well that night?
-> Yes, I slept like a baby.
    <<set $dream_quality to "high">>
    << jump PLACE>>
-> It was a normal night's sleep
    <<set $dream_quality to "average">>
    << jump PLACE>>
-> No, I was tossing and turning all night.
    <<set $dream_quality to "low">>
    << jump PLACE>>
===
title: PLACE
tags: 
colorID: 0
position: 1513,-845
---
<<if $dream_quality is "high">>
    You must feel well rested!
<<elseif $dream_quality is "average">>
    Glad, you got some rest!
<<elseif $dream_quality is "low" >>
    Hope you get some rest soon!
<<endif>>
When did the dream take place?
-> In the past.
    <<set $dream_time to "past">>
    << jump PERSON>>
-> In the present.
    <<set $dream_time to "present">>
    << jump PERSON>>
-> In the future.
    <<set $dream_time to "future">>
    << jump PERSON>>
===
title: PERSON
tags: 
colorID: 0
position: 1679,-1010
---
Was anyone there with you?
-> I was with people who are or were my friends.
    <<set $person to "friend">>
    << jump PERSPECTIVE>>
-> I was with family.
    <<set $person to "family">>
    << jump PERSPECTIVE>>
-> I was with people I don't know very well.
    <<set $person to "acquaintance">>
    << jump PERSPECTIVE>>
===
title: PERSPECTIVE
tags: 
colorID: 0
position: 1896,-1170
---
Were you part of the dream or just observing?
-> I was part of the dream.
    <<set $perspective to "first">>
    <<load_game>>
-> I was just watching the dream.
    <<set $perspective to "third">>
    <<load_game>>
-> I was watching myself in the dream.
    <<set $perspective to "mixed">>
    <<load_game>>

===
title: JOURNAL1
tags: 
colorID: 0
position: 2246,174
---
Are there any qualities about the {$person} from your dream that [select value={$dream_mood} positive="you like" neutral="you don't care for" negative="you really dislike" /]?
-> Yes.
    <<set $person_mood_connection to true>>
    <<jump PERSONPERSPECTIVE>>
-> No.
    <<set $person_mood_connection to false>>
    <<jump PERSONPERSPECTIVE>>
===
title: PERSONPERSPECTIVE
tags: 
colorID: 0
position: 2486,-267
---
Do you feel [select value={$perspective} first="close to" third="disconnected from" mixed="like your relationship is changing with" /] the {$person} in your dream?
-> Yes.
    <<set $person_pov_connection to true>>
    << jump JOURNAL1END>>
-> No.
    <<set $person_pov_connection to false>>
    << jump JOURNAL1END>>
===
title: JOURNAL1END
tags: 
colorID: 0
position: 2726,-709
---
<<if $person_mood_connection is true>>
	The {$person} in your dream seems to be related to the {$dream_mood} tone of your dream. How could that person represent a shared quality about yourself that is making you feel [select value={$dream_mood} positive="happy" negative="sad" neutral="detached" /]?
<<else>>
	The {$person} in your dream doesn't seems to be related to the {$dream_mood} tone of your dream.
<<endif>>

<<if $person_pov_connection is true>>
	The {$person} in your dream seems to be connected to the {$perspective}-person perspective of your dream. Could that person represent a part of your life that you feel [select value={$perspective} first="is repeating itself or happening again" third="like you've moved on from" mixed="has passed but you still miss it" /]?
<<else>>
	The {$person} in your dream doesn't seems to be connected to the {$perspective}-person perspective of your dream.
<<endif>>
<<close_journal JournalCheckpoint1>>
===
title: JOURNAL2
tags: 
colorID: 0
position: 3216,178
---
Do you feel [select value={$dream_mood} positive="optimistic about" neutral="confused about" negative="worried or unproud of" /] the {$dream_time} event that took place in your dream?
-> Yes.
    <<set $time_mood_connection to true>>
    << jump TIMEPERSPECTIVE>>
-> No.
    <<set $time_mood_connection to false>>
    << jump TIMEPERSPECTIVE>>
===

title: TIMEPERSPECTIVE
tags: 
colorID: 0
position: 3454,-261
---
Do you feel that the {$dream_time} event that took place in your dream [select value={$perspective} first="is vital" third="is unimportant" mixed="could be important" /] to who you are now?
-> Yes.
    <<set $time_pov_connection to true>>
    << jump JOURNAL2END>>
-> No.
    <<set $time_pov_connection to false>>
    << jump JOURNAL2END>>
===
title: JOURNAL2END
tags: 
colorID: 0
position: 3694,-701
---
<<if $time_mood_connection is true>>
	The {$dream_time} setting of your dream seems to be related to the {$dream_mood} tone of your dream. How could that setting represent a part about yourself that is making you feel [select value={$dream_mood} positive="happy" negative="sad" neutral="detached" /]?
<<else>>
	The {$dream_time} setting of your dream doesn't seems to be related to the {$dream_mood} tone of your dream.
<<endif>>

<<if $time_pov_connection is true>>
	The {$dream_time} setting of your dream seems to be connected to the {$perspective}-person perspective of your dream. Could that setting represent a part of your life that you feel [select value={$perspective} first="is repeating itself or happening again" third="like you've moved on from" mixed="has passed but you still miss it" /]?
<<else>>
	The {$dream_time} setting of your dream doesn't seems to be connected to the {$perspective}-person perspective of your dream.
<<endif>>
<<close_journal JournalCheckpoint2>>
===
title: JOURNAL3
tags: 
colorID: 0
position: 4246,178
---
<<if $dream_quality is "high">>
    Given that you've been sleeping well, I would say that despite the things on your mind you are processing and dealing with them well. 
<<elseif $dream_quality is "low">>
    Given that you haven't been sleeping well, I would say that the things on your mind you bothering you and might feel unresolved.
<<else>>
	Since you've had average sleep, it's hard to say if these dreams represent things that are bothering you or not.
<<endif>>

<<if $person_mood_connection is true ||  $person_pov_connection is true ||  $time_mood_connection is true ||  $time_pov_connection is true>>
    We found some connections between the aspects you wrote about in your dreams. I hope I encouraged you to think about them more and that it can be insightful.
<<endif>>

<<if $person_mood_connection is false && $person_pov_connection is false && $time_mood_connection is false && $time_pov_connection is false>>
    We didn't find any connections between the aspects in your dream that we wrote about, and that's okay. There might be other parts of you dream that are worth thinking about.
<<endif>>

-> Sure
    << jump JOUNRAL3END>>
===
title: JOUNRAL3END
tags: 
colorID: 0
position: 4366,-42
---
We can't always control our dreams or our feelings. But we can control how we respond to them and what they mean to us.
I hope this journal was a helpful exercise and that if something is weighting heavy on your mind that you'll give yourself the space to express and acknowledge it. Writing in a journal can be a good way to do that!
<<close_journal JournalCheckpoint3>>
===
